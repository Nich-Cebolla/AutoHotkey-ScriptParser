
class ScriptParser_GetIncluded {
    static __New() {
        this.DeleteProp('__New')
        proto := this.Prototype
        proto.Encoding := ''
        proto.PatternCountLines := 'S)(?:[\r\n]+|^)(?:[ \t]*;.*|[ \t]*/\*[\w\W]*?\*/|[ \t]+)'
    }
    /**
     * @description - Processes a relative path with any number of ".\" or "..\" segments.
     * @param {VarRef} Path - A variable containing the relative path to evaluate as string.
     * @param {String} [RelativeTo] - The location `Path` is relative to. If unset, the working directory
     * is used. `RelativeTo` can also be relative with "..\" leading segments.
     */
    static ResolveRelativePathRef(&Path, RelativeTo?) {
        if IsSet(RelativeTo) && RelativeTo {
            SplitPath(RelativeTo, , , , , &Drive)
            if !Drive {
                if InStr(RelativeTo, '.\') {
                    w := A_WorkingDir
                    _Process(&RelativeTo, &w)
                } else {
                    RelativeTo := A_WorkingDir '\' RelativeTo
                }
            }
        } else {
            RelativeTo := A_WorkingDir
        }
        if InStr(Path, '.\') {
            _Process(&Path, &RelativeTo)
        } else {
            Path := RelativeTo '\' Path
        }

        _Process(&path, &relative) {
            split := StrSplit(path, '\')
            segments := []
            segments.Capacity := split.Length
            path := ''
            i := 0
            for s in split {
                if s == '.' {
                    continue
                } else if s == '..' {
                    if Segments.Length {
                        segments.RemoveAt(-1)
                    } else {
                        relative := SubStr(relative, 1, InStr(relative, '\', , , -1) - 1)
                    }
                } else {
                    segments.Push(A_Index)
                }
            }
            if segments.Length {
                for i in segments {
                    path .= '\' split[i]
                }
                if relative {
                    path := relative path
                } else {
                    _Throw()
                }
            } else if relative {
                path := relative
            } else {
                _Throw()
            }
        }
        _Throw() {
            throw ValueError('Invalid input parameters.', -2)
        }
    }

    /**
     * @classdesc - Reads a file and identifies each #include or #IncludeAgain statement. Resolves the path to
     * each included file.
     *
     * Use this to get the file paths associated with all of the #include or #IncludeAgain statements
     * in a script, optionally recursing into the included files.
     *
     * There are a few properties of interest:
     * - {@link ScriptParser_GetIncluded#Result} - An array of {@link ScriptParser_GetIncluded.File} objects, one for
     *   each #include or #IncludeAgain statement encountered during processing and that was associated with a
     *   file path for which `FileExist` returned zero.
     * - {@link ScriptParser_GetIncluded#NotFound} - An array of {@link ScriptParser_GetIncluded.File} objects, one for
     *   each #include or #IncludeAgain statement encountered during processing and that was associated
     *   with a file path for which `FileExist` returned zero.
     * - {@link ScriptParser_GetIncluded.Prototype.Unique} - Call this to get a map where each key is a full file
     *   path and each value is an array of {@link ScriptParser_GetIncluded.File} objects, each representing
     *   an #include or #IncludeAgain statement that resolved to the same file path.
     * - {@link ScriptParser_GetIncluded.Prototype.CountLines} - Returns the number of lines of code in a project.
     *
     * One {@link ScriptParser_GetIncluded.File} is created for each #include or #IncludeAgain statement, but
     * each individual file is only read a maximum of one time.
     *
     * {@link ScriptParser_GetIncluded.File} has the following properties:
     * - Children - An array of {@link ScriptParser_GetIncluded.File} objects representing #include or #IncludeAgain statements in the file.
     * - FullPath - The full path to the file.
     * - Ignore - Returns 1 if the #include or #IncludeAgain statement had the *i option. Returns 0 otherwise.
     * - IsAgain - Returns 1 if it was an #IncludeAgain statement. Returns 0 if it was an #include statement.
     * - Line - The line number on which the #include or #IncludeAgain statement was encountered.
     * - Match - The `RegExMatchInfo` object generated during processing.
     * - Name - The file name without extension of the file.
     * - Parent - The full path of the script that contained the #include or #IncludeAgain statement.
     * - Path - The unmodified path string from the script's content.
     *
     * The first item in the {@link ScriptParser_GetIncluded#Result} array will not have all of the properties
     * because it will be an item created from the path passed to the `Path` parameter.
     *
     * @param {String} Path - The path to the file to analyze. If a relative path is provided, it
     * is assumed to be relative to the current working directory.
     * @param {Boolean} [Recursive = true] - If true, recursively processes all included files.
     * If a file is encountered more than once, a {@link ScriptParser_GetIncluded.File} object is generated
     * for that encounter but the file does not get processed again.
     * @param {String} [ScriptDir = ""] - The path to the local library as described in the
     * {@link https://www.autohotkey.com/docs/v2/Scripts.htm#lib documentation}. This would be
     * the equivalent of `A_ScriptDir "\lib"` when the script is actually running. Since this function
     * is likely to be used outside of the script's context, the local library must be provided if it
     * is to be included in the search.
     * @param {String} [AhkExeDir = ""] - The path to the standard library as described in the
     * {@link https://www.autohotkey.com/docs/v2/Scripts.htm#lib documentation}. This would be the
     * equivalent of `A_AhkPath "\lib"` when the script is actually running. Since this function is
     * likely to be used outside of the script's context, the standard library must be provided if it
     * is to be included in the search.
     * @param {String} [Encoding] - The file encoding to use when reading the files.
     *
     * @returns {ScriptParser_GetIncluded}
     */
    __New(Path, Recursive := true, ScriptDir := '', AhkExeDir := '', Encoding?) {
        if !FileExist(Path) {
            throw Error('File not found.', , Path)
        }
        constructor := ScriptParser_GetIncluded.File
        ResolveRelativePath := ObjBindMethod(ScriptParser_GetIncluded, 'ResolveRelativePathRef')
        /**
         * An array of {@link ScriptParser_GetIncluded.File} objects, one for each #include or #IncludeAgain
         * statement encountered during processing and that was associated with a file path for
         * which `FileExist` returned nonzero. If `FileExist` returned zero, the item was added to
         * {@link ScriptParser_GetIncluded#NotFound}.
         * @memberof ScriptParser_GetIncluded
         * @instance
         * @type {ScriptParser_GetIncluded.File[]}
         */
        this.Result := []
        /**
         * An array of {@link ScriptParser_GetIncluded.File} objects, one for each #include or #IncludeAgain
         * statement encountered during processing and that was associated with a file path for
         * which `FileExist` returned zero. If `FileExist` returned nonzero, the item was added to
         * {@link ScriptParser_GetIncluded#NotFound}.
         * @memberof ScriptParser_GetIncluded
         * @instance
         * @type {ScriptParser_GetIncluded.File[]}
         */
        this.NotFound := []
        If IsSet(Encoding) {
            this.Encoding := Encoding
        }
        result := this.Result
        notFound := this.NotFound
        read := Map()
        read.CaseSense := false
        SplitPath(Path, , , , , &drive)
        if !drive {
            ResolveRelativePath(&Path)
        }
        pending := [ constructor('', path, '', 0) ]
        result.Push(pending[-1])
        result.Capacity := pending.Capacity := notFound.Capacity := Recursive ? 32 : 64
        if ScriptDir {
            libDirs := [ ScriptDir ]
        } else {
            libDirs := [ ]
        }
        libDirs.Push(A_MyDocuments '\AutoHotkey\lib')
        if AhkExeDir {
            libDirs.Push(AhkExeDir)
        }
        if Recursive {
            loop {
                if !pending.Length {
                    break
                }
                active := pending.Pop()
                SplitPath(active.FullPath, , &cwd)
                read.Set(active.FullPath, 1)
                ct := 0
                f := FileOpen(active.FullPath, 'r', Encoding ?? unset)
                loop {
                    if f.AtEoF {
                        f.Close()
                        break
                    }
                    ct++
                    line := f.ReadLine()
                    if RegExMatch(line, 'iS)^[ \t]*\K#include(?<again>again)?[ \t]+(?<i>\*i[ \t]+)?(?:<(?<lib>[^>]+)>|(?<path>.+))', &match) {
                        if _path := match['path'] {
                            _path := Trim(StrReplace(_path, '``;', ';'), '"')
                            if RegExMatch(_path, '[ \t]+;.*', &match_comment) {
                                _path := StrReplace(_path, match_comment[0], '')
                            }
                            while RegExMatch(_path, 'iS)%(A_(?:AhkPath|AppData|AppDataCommon|'
                            'ComputerName|ComSpec|Desktop|DesktopCommon|IsCompiled|LineFile|MyDocuments|'
                            'ProgramFiles|Programs|ProgramsCommon|ScriptDir|ScriptFullPath|ScriptName|'
                            'Space|StartMenu|StartMenuCommon|Startup|StartupCommon|Tab|Temp|UserName|'
                            'WinDir))%', &match_a) {
                                _path := StrReplace(match_a[0], %match_a[1]%)
                            }
                            SplitPath(_path, , , &ext, , &drive)
                            if !drive {
                                ResolveRelativePath(&_path, cwd)
                            }
                            ; If it is a file path
                            if ext {
                                _Add(_path)
                            } else {
                                ; change the current working directory
                                cwd := _path
                            }
                        } else {
                            lib := match['lib']
                            loop 2 {
                                for dir in libDirs {
                                    if FileExist(dir '\' lib '.ahk') {
                                        _Add(dir '\' lib '.ahk')
                                        continue 3
                                    }
                                }
                                lib := SubStr(lib, 1, InStr(lib, '_') - 1)
                            }
                        }
                    }
                }
            }
        } else {
            active := pending.Pop()
            SplitPath(active.FullPath, , &cwd)
            ct := 0
            f := FileOpen(active.FullPath, 'r', Encoding ?? unset)
            loop {
                if f.AtEoF {
                    f.Close()
                    break
                }
                ct++
                line := f.ReadLine()
                if RegExMatch(line, 'iS)^[ \t]*\K#include(?<again>again)?[ \t]+(?:<(?<lib>[^>]+)>|(?<path>.+))', &match) {
                    if _path := match['path'] {
                        _path := Trim(StrReplace(_path, '``;', ';'), '"')
                        if RegExMatch(_path, '[ \t]+;.*', &match_comment) {
                            _path := StrReplace(_path, match_comment[0], '')
                        }
                        while RegExMatch(_path, 'iS)%(A_(?:AhkPath|AppData|AppDataCommon|'
                        'ComputerName|ComSpec|Desktop|DesktopCommon|IsCompiled|LineFile|MyDocuments|'
                        'ProgramFiles|Programs|ProgramsCommon|ScriptDir|ScriptFullPath|ScriptName|'
                        'Space|StartMenu|StartMenuCommon|Startup|StartupCommon|Tab|Temp|UserName|'
                        'WinDir))%', &match_a) {
                            _path := StrReplace(match_a[0], %match_a[1]%)
                        }
                        SplitPath(_path, , , &ext, , &drive)
                        if !drive {
                            ResolveRelativePath(&_path, cwd)
                        }
                        ; If it is a file path
                        if ext {
                            _Add(_path)
                        } else {
                            ; change the current working directory
                            cwd := _path
                        }
                    } else {
                        lib := match['lib']
                        loop 2 {
                            for dir in libDirs {
                                if FileExist(dir '\' lib '.ahk') {
                                    _Add(dir '\' lib '.ahk')
                                    continue 3
                                }
                            }
                            lib := SubStr(lib, InStr(lib, '_') + 1)
                        }
                    }
                }
            }
            f.Close()
        }

        return

        _Add(fullPath) {
            item := constructor(match, fullPath, active.FullPath, ct)
            active.Children.Push(item)
            if FileExist(fullPath) {
                result.Push(item)
                if !read.Has(fullPath) {
                    pending.Push(item)
                }
            } else {
                notFound.Push(item)
            }
        }
    }

    /**
     * Counts the lines of code in the project. Consecutive line breaks are replaced with
     * a single line break before counting. Each individual file is only processed once.
     *
     * @param {Boolean} [CodeLinesOnly = true] - If true, lines that only have a comment are not
     * included in the count.
     *
     * @returns {Integer} - The number of lines.
     */
    CountLines(CodeLinesOnly := true) {
        ct := 0
        if CodeLinesOnly {
            pattern := this.PatternCountLines
            if encoding := this.Encoding {
                for path in this.GetUnique() {
                    StrReplace(RegExReplace(RegExReplace(FileRead(path, encoding), pattern, '`n'), '\R+', '`n'), '`n', , , &n)
                    ct += n + 1
                }
            } else {
                for path in this.GetUnique() {
                    StrReplace(RegExReplace(RegExReplace(FileRead(path), pattern, '`n'), '\R+', '`n'), '`n', , , &n)
                    ct += n + 1
                }
            }
        } else {
            if encoding := this.Encoding {
                for path in this.GetUnique() {
                    StrReplace(RegExReplace(FileRead(path, encoding), '\R+', '`n'), '`n', , , &n)
                    ct += n + 1
                }
            } else {
                for path in this.GetUnique() {
                    StrReplace(RegExReplace(FileRead(path), '\R+', '`n'), '`n', , , &n)
                    ct += n + 1
                }
            }
        }
        return ct
    }

    GetUnique() {
        if !this.HasOwnProp('Unique') {
            /**
             * A map where each key is a full file path and each value is an array of {@link ScriptParser_GetIncluded.File}
             * objects, each representing an #include or #IncludeAgain statement that resolved to the
             * same file path.
             * @memberof ScriptParser_GetIncluded
             * @instance
             * @type {Map}
             */
            this.Unique := Map()
            unique := this.Unique
            unique.CaseSense := false
            for item in this.Result {
                if unique.Has(item.FullPath) {
                    unique.Get(item.FullPath).Push(item)
                } else {
                    unique.Set(item.FullPath, [ item ])
                }
            }
        }
        return this.Unique
    }
    /**
     * Constructs a string of the contents of the file passed to the parameter `Path`, recursively
     * replacing each #include and #IncludeAgain statement with the content from the appropriate file.
     * The created string is set to property {@link ScriptParser_GetIncluded#Content}.
     */
    Build(Encoding?) {
        return this.__Content := this.Result[1].Build(Encoding ?? unset)
    }

    class File {
        __New(match, fullPath, parent, line) {
            this.Match := match
            this.FullPath := fullPath
            SplitPath(fullPath, , , , &name)
            this.Name := name
            this.Line := line
            this.Parent := parent
            this.Children := []
        }
        /**
         * Constructs a string of the file's contents, recursively replacing each #include and
         * #IncludeAgain statement with the content from the appropriate file. The created string
         * is set to property {@link ScriptParser_GetIncluded.File#Content}.
         */
        Build(Encoding?) {
            s := FileRead(this.FullPath, Encoding ?? unset)
            for item in this.Children {
                s := RegExReplace(s, '(?<=[\r\n]|^)\Q' item.Match[0] '\E(?=[\r\n]|$)', item.Build(Encoding ?? unset), , 1)
            }
            return this.__Content := s
        }
        Ignore => this.Match ? this.Match['i'] ? 1 : 0 : 0
        IsAgain => this.Match ? this.Match['again'] ? 1 : 0 : 0
        Path => this.Match ? this.Match['path'] : ''
    }
}
